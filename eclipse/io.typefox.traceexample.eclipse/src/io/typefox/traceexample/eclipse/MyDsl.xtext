/*******************************************************************************
 * Copyright (c) 2017 TypeFox (http://www.typefox.io) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
grammar io.typefox.traceexample.eclipse.MyDsl with org.eclipse.xtext.common.Terminals

generate myDsl "http://www.typefox.io/traceexample/eclipse/MyDsl"

Model:
	types+=ClassDeclaration*;

ClassDeclaration:
	'class' name=ID '{'
		members+=Member*
	'}';

Member:
	Operation | Property;

Property:
	name=ID ':' type=TypeRef;

Operation:
	name=ID '(' (parameters+=Parameter (',' parameters+=Parameter)*)? ')' ':' type=TypeRef '{'
		expression=Expression?
	'}';

TypeRef:
	declaration=[ClassDeclaration];

Parameter:
	name=ID ':' type=TypeRef;

Feature:
	Member | Parameter;

Expression:
	FeatureCall (
		{FeatureCall.receiver=current} '.' feature=[Feature] (parenthesized?='(' (parameters+=Expression (',' parameters+=Expression)*)? ')')?
	)*;

FeatureCall:
	feature=[Feature] (parenthesized?='(' (parameters+=Expression (',' parameters+=Expression)*)? ')')?;
